set shell := ["bash", "-ueo", "pipefail", "-c"]
set quiet
set ignore-comments

LOG_LEVEL  := "trace"
APP        := "./target/release/poc"
SUDO       := "/usr/bin/sudo -E env" # NOTE: Full path to sudo for `nix develop --ignore-environment`
HPING      := `which hping3`
PYTEST     := `which pytest`
BENCH_BASE := SUDO + "RUST_LOG='poc=info' APP=" + APP + " HPING=" + HPING

help:
    @just --list

build:
    cargo build

build-release:
    cargo build --release

build-forever:
    cargo watch --clear -x build

run *ARGS:
    RUST_LOG='poc={{LOG_LEVEL}}' cargo run -- {{ARGS}}

run-forever *ARGS:
    RUST_LOG='poc={{LOG_LEVEL}}' cargo watch --clear -x "run -- {{ARGS}}"

run-forever-release *ARGS:
    RUST_LOG='poc={{LOG_LEVEL}}' cargo watch --clear -x "run --release -- {{ARGS}}"

lint-clippy:
    cargo clippy

lint-machete:
    cargo machete

traffic:
    {{SUDO}} {{HPING}} --udp --data 123 127.0.0.1 -i u500000

traffic-fast:
    {{SUDO}} {{HPING}} --udp --data 123 127.0.0.1 --fast

traffic-faster:
    {{SUDO}} {{HPING}} --udp --data 123 127.0.0.1 --faster

traffic-flood:
    timeout 15 {{SUDO}} {{HPING}} --udp --data 123 127.0.0.1 --flood

traffic-12345-fast:
    {{SUDO}} {{HPING}} --udp --data 123 127.0.0.1 -c 12345 --fast

traffic-12345-faster:
    {{SUDO}} {{HPING}} --udp --data 123 127.0.0.1 -c 12345 --faster

bench timeout="1000":
    TIMEOUT={{timeout}} {{BENCH_BASE}} {{PYTEST}} -s -vvv

pipenv-bench timeout="1000":
    TIMEOUT={{timeout}} {{BENCH_BASE}} pipenv run {{PYTEST}} -s -vvv
   
